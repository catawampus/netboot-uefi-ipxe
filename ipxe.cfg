#!ipxe

# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x64 || set arch x86
cpuid --ext 29 && set archl amd64 || set archl i386

console --x 640 --y 480
#console --picture /ipxe/logo.png

# password used to protect the automatic installers
set auto_password 123456

###################### MAIN MENU ####################################

:start
menu ESXi System Installers
item --gap --             ------------------------- Installation ------------------------------
item --key m esxi7man     Install Cisco ESXi 7.0.2 Install
item --key v esxi7        Install Cisco ESXi 7.0.2 Automated Install
item
item
item
item --gap --             ---------------------- Advanced options -----------------------------
#item --key c config       Configure settings
item shell                Drop to iPXE shell
item reboot               Reboot computer
item
item --key x exit         Exit iPXE and continue boot
choose --timeout 5000 --default exit selected || goto cancel
set menu-timeout 0
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start

############ MAIN MENU ITEMS ############

:esxi7
set username
set password
login
iseq ${password} ${auto_password} && goto esxi7pass  || goto start
goto start

:esxi7pass
set esxi_config http://SERVER/esxi/config/boot-7.0.2.php?prefix=/esxi/cisco-7.0.2&kickstart=/esxi/config/ks.php&hostname=${hostname}&ip=${ip}&netmask=${netmask}&gateway=${gateway}&nameserver=${dns}
echo ${esxi_config}
sleep 2
kernel http://SERVER/esxi/cisco-7.0.2/mboot.efi -c ${esxi_config}
boot || goto custom_exit

:esxi7man
set esxi_config http://SERVER/esxi/config/boot-7.0.2.php?prefix=/esxi/cisco-7.0.2&hostname=${hostname}&ip=${ip}&netmask=${netmask}&gateway=${gateway}&nameserver=${dns}
echo ${esxi_config}
sleep 2
kernel http://SERVER/esxi/cisco-7.0.2/mboot.efi -c ${esxi_config}
boot || goto custom_exit


:custom_exit
echo Boot failed. Return to menu
sleep 2
goto start

